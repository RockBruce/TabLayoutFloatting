
if (isModule) {
    apply plugin: 'com.android.library'
    apply plugin: 'kotlin-android'
} else {
    apply plugin: 'com.android.application'    //独立运行条件一
    apply plugin: 'kotlin-android'
}
android {
    compileSdkVersion androidId.compileSdkVersion

    defaultConfig {
        if (!isModule) {
            //组件模块化
            applicationId appId.login    //独立运行条件二
        }
        minSdkVersion androidId.minSdkVersion
        targetSdkVersion androidId.targetSdkVersion
        versionCode androidId.versionCode
        versionName androidId.versionName

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        //给java代码暴露，标记正式环境和测试环境的标记
        //组件化和集成化的时候需要
        buildConfigField("boolean", "isModule", String.valueOf(isModule))
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [moduleName: project.getName()]
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    //资源配置
    sourceSets {
        main {
            if (!isModule) {
                //组件化模块，需要单独运行时
                //单独配置ManiFest.xml文件，组件化开发会用到这个文件
                manifest.srcFile 'src/main/logintest/AndroidManifest.xml'
                //单独配置的源文件下的包名要跟默认源文件包名要一个样，cn.desmall.login
                java.srcDirs 'src/main/logintest/java', 'src/main/java'
                println("app")

            } else {
                //集成化模块，整个项目打包
                manifest.srcFile 'src/main/AndroidManifest.xml'
                println("lib")

            }
        }
    }
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    //注解处理器
    annotationProcessor project(':router-compiler')
    //自定义注解模块
    implementation project(':router-core')

}
